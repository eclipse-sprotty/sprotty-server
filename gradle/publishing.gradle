/********************************************************************************
 * Copyright (c) 2017-2018 TypeFox and others.
 *
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License v. 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * This Source Code may also be made available under the following Secondary
 * Licenses when the conditions for such availability set forth in the Eclipse
 * Public License v. 2.0 are satisfied: GNU General Public License, version 2
 * with the GNU Classpath Exception which is available at
 * https://www.gnu.org/software/classpath/license.html.
 *
 * SPDX-License-Identifier: EPL-2.0 OR GPL-2.0 WITH Classpath-exception-2.0
 ********************************************************************************/

publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
			
			artifact sourcesJar
			artifact javadocJar
			
			pom {
				afterEvaluate {
					groupId project.group
					artifactId project.name
					version project.version
					if (project.hasProperty('title')) {
						name = project.title
						description = project.description
					}
				}
				url = 'https://github.com/eclipse-sprotty/sprotty-server'
				licenses {
					license {
						name = 'Eclipse Public License v2.0'
						url = 'http://www.eclipse.org/legal/epl-2.0'
					}
				}
				scm {
					connection = 'scm:git:git@github.com:eclipse-sprotty/sprotty-server.git'
					developerConnection = 'scm:git:git@github.com:eclipse-sprotty/sprotty-server.git'
					url = 'git@github.com:eclipse-sprotty/sprotty-server.git'
				}
				developers {
					developer {
						name = 'Jan Köhnlein'
						email = 'jan.koehnlein@typefox.io'
						organizationUrl = 'http://typefox.io'
					}
					developer {
						name = 'Miro Spönemann'
						email = 'miro.spoenemann@typefox.io'
						organizationUrl = 'http://typefox.io'
					}
					developer {
						name = 'Tom Vandenbussche'
						email = 'tom.vandenbussche@sigasi.com'
						organizationUrl = 'https://www.sigasi.com/'
					}
				}
			}
		}
	}

	repositories {
		maven {
			name = "OSSRH"
			def releasesRepoUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
			def snapshotsRepoUrl = 'https://oss.sonatype.org/content/repositories/snapshots/'
			credentials {
				username = System.getenv('SONATYPE_USERNAME') ?: ''
				password = System.getenv('SONATYPE_PASSWORD') ?: ''
			}
			url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
			// use this URL for local testing/debugging purposes			
			// url = "file:" + file("${rootDir}/build/maven-repository")
		}
	}

	signing {
		// Use signing.gnupg.passphrase property to bypass the secret key passphrase
		useGpgCmd()
		sign publishing.publications.mavenJava
	}

	javadoc {
		if (JavaVersion.current().isJava9Compatible()) {
			options.addBooleanOption('html5', true)
		}
	}
}
