/********************************************************************************
 * Copyright (c) 2017-2018 TypeFox and others.
 *
 * This program and the accompanying materials are made available under the
 * terms of the Eclipse Public License v. 2.0 which is available at
 * http://www.eclipse.org/legal/epl-2.0.
 *
 * This Source Code may also be made available under the following Secondary
 * Licenses when the conditions for such availability set forth in the Eclipse
 * Public License v. 2.0 are satisfied: GNU General Public License, version 2
 * with the GNU Classpath Exception which is available at
 * https://www.gnu.org/software/classpath/license.html.
 *
 * SPDX-License-Identifier: EPL-2.0 OR GPL-2.0 WITH Classpath-exception-2.0
 ********************************************************************************/

import java.time.format.DateTimeFormatter
import java.time.LocalDateTime

buildscript {
	repositories {
		mavenCentral()
		maven {
			url "https://plugins.gradle.org/m2/"
		}
	}
	dependencies {
		// cs: version 6.4 of this plugin uses a gradle API marked as @deprecated, version 7.0.+ requires Java-17, so once Java is upgraded, upgrade this dependency, too!
		classpath "biz.aQute.bnd:biz.aQute.bnd.gradle:6.4.+"
		classpath 'org.xtext:xtext-gradle-plugin:4.0.+'
	}
}

ext.buildTime = DateTimeFormatter.ofPattern('yyyyMMdd-HHmm').format(LocalDateTime.now())

apply from: "${rootDir}/gradle/versions.gradle"

configure(subprojects.findAll {it.file('src').exists()}) {
	repositories {
		maven {
			name 'maven-repo'
			url 'https://repo.maven.apache.org/maven2/'
		}
	}

	group = 'org.eclipse.sprotty'
	version = rootProject.version

	apply plugin: 'java-library'
	apply plugin: 'org.xtext.xtend'
	apply plugin: 'eclipse'

	apply from: "${rootDir}/gradle/java-compiler-settings.gradle"

	if (name.contains('example')) {
		javadocJar.enabled = false
	} else {
		apply plugin: 'maven-publish'
		apply from: "${rootDir}/gradle/maven-deployment.gradle"
		apply from: "${rootDir}/gradle/manifest-gen.gradle"
	}
}

task clean(type: Delete) {
	group 'Build'
	description 'Deletes the local repositories'
	delete 'build'
}
